apply plugin: 'maven'
apply plugin: 'signing'

// Define how we publish artifacts to Maven.
// Derived from instructions on the Central Repository site:
// http://central.sonatype.org/pages/gradle.html
group = "net.alloyggp"
version = "0.0.2"
task javadocJar(type: Jar) {
    classifier = 'javadoc'
    from javadoc
}
task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}
artifacts {
    archives javadocJar, sourcesJar
}
signing {
    sign configurations.archives
}
uploadArchives {
  repositories {
    mavenDeployer {
      beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

      repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
        authentication(userName: ossrhUsername, password: ossrhPassword)
      }

      snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
        authentication(userName: ossrhUsername, password: ossrhPassword)
      }

      pom.project {
        name 'escape-rope'
        packaging 'jar'
        artifactId = "escape-rope"
        description 'A library for turning structured data into strings and back.'
        url 'https://github.com/AlexLandau/escape-rope'

        scm {
          connection 'scm:git:git@github.com:AlexLandau/escape-rope.git'
          developerConnection 'scm:git:git@github.com:AlexLandau/escape-rope.git'
          url 'git@github.com:AlexLandau/escape-rope.git'
        }

        licenses {
          license {
            name 'Apache License 2.0'
            url 'http://www.apache.org/licenses/LICENSE-2.0'
          }
        }

        developers {
          developer {
            id 'AlexLandau'
            name 'Alex Landau'
            email 'alex@alloyggp.net'
          }
        }
      }
    }
  }
}
